; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName = Osadníci - Hlídáček
AppVersion = 1.0
AppPublisher = Miloš Prchlík
AppPublisherURL = http://osadnici-test.happz.cz/
AppContact = osadnici@happz.cz
DefaultDirName = {pf}\settlers-notify
DefaultGroupName = Osadníci - Hlídáček
Compression = lzma/ultra64
OutputDir = dist\

[Languages]
Name: "cz"; MessagesFile: "compiler:Languages\Czech.isl"

[Files]
Source: "dist\favicon.ico"; DestDir: "{app}";
Source: "dist\IronPython.dll"; DestDir: "{app}";
Source: "dist\IronPython.Modules.dll"; DestDir: "{app}";
Source: "dist\Microsoft.Dynamic.dll"; DestDir: "{app}";
Source: "dist\Microsoft.Scripting.dll"; DestDir: "{app}";
Source: "dist\Microsoft.Scripting.Metadata.dll"; DestDir: "{app}";
Source: "dist\settlers-notify.dll"; DestDir: "{app}";
Source: "dist\settlers-notify.exe"; DestDir: "{app}";

[Types]
Name: "full"; Description: "Plná instalace"

[Components]
Name: "main"; Description: "Main Files"; Types: full; Flags: fixed

[Tasks]
Name: desktopicon; Description: "Vytvořit ikonu na Ploše"; GroupDescription: "Další ikony:"; Components: main

[Icons]
Name: "{commondesktop}\Osadníci - Hlídáček"; Filename: "{app}\settlers-notify.exe"; IconFilename: "{app}\favicon.ico";

[UninstallDelete]
Type: files; Name: "{app}\settlers.conf";

[Code]
function IsDotNetDetected(version: string; service: cardinal): boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1.4322'     .NET Framework 1.1
//    'v2.0.50727'    .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key: string;
    install, serviceCount: cardinal;
    success: boolean;
begin
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + version;
    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;
    // .NET 4.0 uses value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;
    result := success and (install = 1) and (serviceCount >= service);
end;

function InitializeSetup(): Boolean;
begin
    if not IsDotNetDetected('v4\Client', 0) then begin
        MsgBox('Osadníci - Hlídáček vyžaduje Microsoft .NET Framework 4.0 Client Profile.'#13#13
            'Nainstalujte s pomocí Windows Update tuto či novější verzi'#13
            'a spusťte instalaci znovu.', mbInformation, MB_OK);
        result := false;
    end else
        result := true;
end;
